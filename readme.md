<message>     ::= [ ':' <prefix> <SPACE> ] <command> <params> <crlf>
<prefix>      ::= <servername> | ( <nick> [ '!' <user> ] [ '@' <host> ] )
<command>     ::= <letter> { <letter> } | <number> <number> <number>
<SPACE>       ::= ' ' { ' ' }
<params>      ::= <SPACE> [ <target> | ':' <trailing> | <middle> <params> ]
<middle>      ::= <non-empty-sequence-of-octets-not-including-SPACE-NUL-CR-LF>
<trailing>    ::= <sequence-of-octets-not-including-NUL-CR-LF>
<crlf>        ::= CR LF
<user>        ::= <nonwhite> { <nonwhite> }
<letter>      ::= 'a' ... 'z' | 'A' ... 'Z'
<number>      ::= '0' ... '9'
<special>     ::= '-' | '[' | ']' | '\' | '`' | '^' | '{' | '}'
<target>      ::= <to> [ "," <target> ]
<to>          ::= <channel> | <user> '@' <servername> | <nick> | <mask>
<channel>     ::= ('#' | '&') <chstring>
<servername>  ::= <host>
<host>        ::= see-RFC-952-details
<nick>        ::= <letter> { <letter> | <number> | <special> }
<mask>        ::= ('#' | '$') <chstring>
<chstring>    ::= <any-8bit-code-except-SPACE-BELL-NUL-CR-LF-comma>
<nonwhite>    ::= <any-8bit-code-except-SPACE-NUL-CR-LF>
